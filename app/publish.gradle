apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.4'
group 'com.gvm'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/app-release.aar")
            groupId
            artifactId 'camera-module'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'gvm'
        name = 'camera-module'
        licenses = ['Apache-2.0']
        description = 'Camera Module SDK'
        websiteUrl = 'https://womantalk.com'
        vcsUrl = 'https://github.com/vinswilliam/wtcamand'
        dryRun = false
        publish = true
        publicDownloadNumbers = true
        version {
            name = this.version
            desc = "Camera Module ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}